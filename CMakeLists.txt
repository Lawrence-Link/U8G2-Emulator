cmake_minimum_required(VERSION 3.16)
project(U8g2Emulator LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# 必须放在前面，这样 Qt 自动 MOC/UIC/RCC 才生效
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTORCC ON)

option(WITH_QT "Enable Qt GUI mode" OFF)

# ==============================
# 1. u8g2 submodule
# ==============================
add_subdirectory(third_party/u8g2 EXCLUDE_FROM_ALL)

# ==============================
# 2. Source files
# ==============================
set(SOURCES
    src/main.cpp
    src/emulator_headless.cpp
    src/emulator_arg_parser.cpp
    src/u8g2_wrapper.cpp
)

set(HEADERS
    include/emulator_arg_parser.h
    include/u8g2_wrapper.h
    include/emulator_headless.h
)

if(WITH_QT)
    find_package(Qt6 COMPONENTS Core Gui Widgets REQUIRED)

    list(APPEND SOURCES
        src/MainWindow.cpp
        src/EmuWorker.cpp
    )

    list(APPEND HEADERS
        include/MainWindow.h
        include/EmuWorker.h
    )

    add_executable(u8g2_emulator ${SOURCES} ${HEADERS})

    target_link_libraries(u8g2_emulator
        PRIVATE
        u8g2
        Qt6::Core
        Qt6::Gui
        Qt6::Widgets
    )

    target_compile_definitions(u8g2_emulator PRIVATE USE_QT_GUI)
else()
    add_executable(u8g2_emulator ${SOURCES} ${HEADERS})
    target_link_libraries(u8g2_emulator PRIVATE u8g2)
endif()

# ==============================
# 3. 头文件路径
# ==============================
target_include_directories(u8g2_emulator PRIVATE
    include
    third_party/u8g2/csrc
)
